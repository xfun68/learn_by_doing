apply plugin: 'idea'
apply plugin: 'application'
apply plugin: 'java'


repositories {
  mavenCentral()
}


sourceSets {
  integrationTest {
    java {
      compileClasspath += main.output + test.output
      runtimeClasspath += main.output + test.output
      srcDir file('src/integration-test/java')
    }
    resources.srcDir file('src/integration-test/resources')
  }
}


configurations {
  integrationTestCompile.extendsFrom testCompile
  integrationTestRuntime.extendsFrom testRuntime
}


dependencies {
  compile 'log4j:log4j:1.2.17'
  testCompile 'junit:junit:4.11'
  integrationTestCompile 'org.assertj:assertj-core:3.0.0'
}


mainClassName = 'com.example.helloworld.HelloWorld'


task copyLicense {
  outputs.file new File("$buildDir/LICENSE")
  doLast {
    copy {
      from "LICENSE"
      into "$buildDir"
    }
  }
}

applicationDistribution.from(copyLicense) {
  into ""
}


task integrationTest(type: Test) {
  testClassesDir = sourceSets.integrationTest.output.classesDir
  classpath = sourceSets.integrationTest.runtimeClasspath
  outputs.upToDateWhen { false }
}

check.dependsOn integrationTest
integrationTest.mustRunAfter test


tasks.withType(Test) {
  reports.html.destination = file("${reporting.baseDir}/${name}")
}
